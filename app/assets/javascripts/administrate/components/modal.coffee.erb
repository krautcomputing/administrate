modalSelector = '#modal'

@loadRemoteUrlInModal = (urlOrIndex, showLoading = false, errorCallback = null) ->
  urls = $(modalSelector).data('urls') || []
  if Number.isInteger(urlOrIndex)
    urlIndex = urlOrIndex
  else
    urlIndex = $(modalSelector).data('urlIndex') || 0
    urls.splice urlIndex + 1, urls.length - urlIndex, urlOrIndex
    $(modalSelector).data 'urls', urls
    urlIndex = urls.length - 1
  $('.back',    modalSelector).toggle urlIndex > 0
  $('.forward', modalSelector).toggle urlIndex < urls.length - 1
  $(modalSelector).data 'urlIndex', urlIndex

  url = urls[urlIndex]
  $('.open', modalSelector).attr 'href', url

  $modalBody = $('.modal-body', modalSelector)
  if showLoading
    $modalBody.text('Loading, please wait...')
  $modalBody.load url, (_, status, xhr) ->
    if status == 'error'
      if errorCallback?
        errorCallback.call()
      alert "Error loading #{url}\n\n#{xhr.status} #{xhr.responseJSON?.error || xhr.statusText}"
    else
      $(@).find('a[data-method="post"], form').attr('data-remote', 'true')
      $(@).parents('.modal').trigger('content-loaded')

$(document)

  .on 'show.bs.modal', modalSelector, (e) ->
    if e.relatedTarget?
      url = $(e.relatedTarget).data('url')
      loadRemoteUrlInModal url, true

  .on 'hide.bs.modal', modalSelector, ->
    $(modalSelector).data 'urls', []

  .on 'click', "#{modalSelector} a.disabled", (e) ->
    e.preventDefault()

  .on 'click', "#{modalSelector} a:not([data-method]):not([data-remote]):not([href='#']):not([href^='slack:']):not([href^='mailto:']):not([href$='.jpg']):not([href$='.png']):not(.disabled)", (e) ->
    unless e.metaKey || $(e.currentTarget).parents('.modal-nav').length
      e.preventDefault()
      $(e.currentTarget)
        .data('original-text', $(e.currentTarget).text())
        .text('Loading...')
        .addClass('disabled')
      loadRemoteUrlInModal e.currentTarget.href, false, ->
        $(e.currentTarget)
          .text($(e.currentTarget).data('original-text'))
          .removeClass('disabled')

  .on 'click', "#{modalSelector} a[data-target='#{modalSelector}']", ->
    # When a modal toggle link is clicked inside a modal, trick Bootstrap
    # into thinking that the modal is not yet open by removing the data
    # from the element.
    $(modalSelector).data('bs.modal', null)

  .on 'ajax:success', modalSelector, (e, data, status, xhr) ->
    unless data.next is undefined
      if data.next?.length
        loadRemoteUrlInModal data.next
      else
        $('#modal').modal('hide')

    notice = xhr.getResponseHeader('<%= Administrate::NOTICE_RESPONSE_HEADER %>')
    if notice?.length
      toastr.success notice

  .on 'ajax:error', modalSelector, (_, xhr) ->
    alert(xhr.responseJSON?.error || xhr.statusText)

  .on 'click', "#{modalSelector} .modal-nav .reload", (e) ->
    e.preventDefault()
    loadRemoteUrlInModal $(modalSelector).data('urlIndex')

  .on 'click', "#{modalSelector} .modal-nav .back", (e) ->
    e.preventDefault()
    loadRemoteUrlInModal $(modalSelector).data('urlIndex') - 1

  .on 'click', "#{modalSelector} .modal-nav .forward", (e) ->
    e.preventDefault()
    loadRemoteUrlInModal $(modalSelector).data('urlIndex') + 1
